
def limamulttrigscanon '{
    local lima_name lima_trig_mode ccd_trig_mode ccd_u taco_dev

    lima_name = ($# > 0) ? "$1" : getval("Lima camera name", "")
    lima_name = _lima_checkccdname(lima_name)
    ccd_u = LIMA_DEV[lima_name]["unit"]
    taco_dev = image_par(ccd_u, "device_id")

    taco_io(taco_dev, "DevCcdResetMultTrigNbFrames")
    LIMA_DEV[lima_name]["prev_mult_trig"] = 0
    LIMA_DEV[lima_name]["mult_trig"] = 1
    lima_trig_mode = _limapar_get(lima_name, "acq_trigger_mode")
    ccd_trig_mode = image_par(ccd_u, "ext_trig")
    if (lima_trig_mode == "INTERNAL_TRIGGER") {
        lima_trig_mode = "INTERNAL_TRIGGER_MULTI"
    } else if (lima_trig_mode == "EXTERNAL_TRIGGER") {
        lima_trig_mode = "EXTERNAL_TRIGGER_MULTI"
	ccd_trig_mode = 2
    }
    _limapar_set(lima_name, "acq_trigger_mode", lima_trig_mode)
    image_par(ccd_u, "ext_trig", ccd_trig_mode)
    CCD_TRIG[ccd_u] = ccd_trig_mode
    
    cdef("user_prescan_head", "lima_mult_trig_head(); ", "lima_mult_trig")
    cdef("user_scan_tail", "lima_mult_trig_tail(0); ", "lima_mult_trig")
}'


def limamulttrigscanoff '{
    local lima_name lima_trig_mode ccd_trig_mode ccd_u i all_off

    lima_name = ($# > 0) ? "$1" : getval("Lima camera name", "")
    lima_name = _lima_checkccdname(lima_name)
    ccd_u = LIMA_DEV[lima_name]["unit"]
    
    LIMA_DEV[lima_name]["mult_trig"] = 0
 
    lima_trig_mode = _limapar_get(lima_name, "acq_trigger_mode")
    ccd_trig_mode = image_par(ccd_u, "ext_trig")
    if (lima_trig_mode == "INTERNAL_TRIGGER_MULTI") {
        lima_trig_mode = "INTERNAL_TRIGGER"
    } else if (lima_trig_mode == "EXTERNAL_TRIGGER_MULTI") {
        lima_trig_mode = "EXTERNAL_TRIGGER"
	ccd_trig_mode = 1
    }
    _limapar_set(lima_name, "acq_trigger_mode", lima_trig_mode)
    image_par(ccd_u, "ext_trig", ccd_trig_mode)
    CCD_TRIG[ccd_u] = ccd_trig_mode

    all_off = 1
    for (i = 1; i <= list_n(LIMA_DEV); i++) {
    	if (list_getpar(LIMA_DEV, i, "mult_trig") > 0)
	    all_off = 0
    }

    if (all_off) {
        cdef("user_prescan_head", "", "lima_mult_trig", "delete")
        cdef("user_scan_tail", "", "lima_mult_trig", "delete")
    }
}'

def lima_mult_trig_head() '{
    local lima_name lima_acq_mode i nb_points msg_printed
    
    lima_acq_mode = _limapar_get(lima_name, "acq_mode")
    if (lima_acq_mode == "ACCUMULATION") {
        printf("Error: %s acq_mode is %s. Mult-trig scan not supported\n", \
	       lima_name, lima_acq_mode)
	exit
    }
    
    nb_points = (_stype & scanType_MeshScan) ? (_n1 * _n2) : _n1
    #nb_points = array_op("rows", SCAN_POINTS)
    msg_printed = 0
    for (i = 1; i <= list_n(LIMA_DEV); i++) {
    	if (list_getpar(LIMA_DEV, i, "mult_trig") > 0) {
	    if (!msg_printed)
                printf("Mult-Trig scan nb_points=%d\n", nb_points)
	    msg_printed = 1
	    lima_name = list_item(LIMA_DEV, i)
	    lima_mult_trig_on(lima_name, nb_points)
	}
    }

    cdef("cleanup_once", "lima_mult_trig_tail(1); ", "lima_mult_trig")
}'

def lima_mult_trig_tail(from_cleanup) '{
    local lima_name i
    
    for (i = 1; i <= list_n(LIMA_DEV); i++) {
    	if (list_getpar(LIMA_DEV, i, "mult_trig") > 0) {
	    lima_name = list_item(LIMA_DEV, i)
	    lima_mult_trig_off(lima_name)
	}
    }

    if (!from_cleanup)
        cdef("cleanup_once", "", "lima_mult_trig", "delete")
}'

def lima_mult_trig_on(lima_name, nb_points) '{
    local ccd_u taco_dev
    if (list_getpar(LIMA_DEV, lima_name, "prev_mult_trig") <= 0) {
        ccd_u = LIMA_DEV[lima_name]["unit"]
        taco_dev = image_par(ccd_u, "device_id")
        taco_io(taco_dev, "DevCcdSetMultTrigNbFrames", nb_points)
        list_setpar(LIMA_DEV, lima_name, "prev_mult_trig", 1)
    }
}'

def lima_mult_trig_off(lima_name) '{
    local ccd_u taco_dev
    if (list_getpar(LIMA_DEV, lima_name, "prev_mult_trig") > 0) {
        ccd_u = LIMA_DEV[lima_name]["unit"]
        taco_dev = image_par(ccd_u, "device_id")
        taco_io(taco_dev, "DevCcdResetMultTrigNbFrames")
        list_setpar(LIMA_DEV, lima_name, "prev_mult_trig", 0)
    }
}'

